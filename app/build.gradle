buildscript {
    repositories {
//        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        // can be removed with android-gradle plugin is upgraded to 2.2
        // https://twitter.com/JakeWharton/status/760836175586267136
//        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
    }

    // Exclude the lombok version that the android plugin depends on.
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.jakewharton.butterknife'

ext {
    okhttpVersion = "3.0.1"
    retrofitVersion = "2.0.0"
    sdkVersion = 24
    supportLibVersion = "24.2.1"
}

dependencies {
    compile "com.android.support:support-v13:${supportLibVersion}"
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"

    compile 'com.github.kaushikgopal:CoreTextUtils:c703fa12b6'
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    compile 'com.jakewharton.timber:timber:2.4.2'
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    compile "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    compile "com.squareup.okhttp3:okhttp-urlconnection:${okhttpVersion}"
    compile 'com.mcxiaoke.volley:library:1.0.19'

    // ----------------------------------
    //  Rx dependencies

    compile 'io.reactivex.rxjava2:rxjava:2.0.1'

    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // libs like rxbinding + rxjava-math haven't been ported to RxJava 2.x yet, so this helps
    compile "com.github.akarnokd:rxjava2-interop:0.6.1"

    compile 'io.reactivex:rxjava-math:1.0.0'
    compile 'com.jakewharton.rxrelay:rxrelay:1.2.0'
    compile 'com.jakewharton.rxbinding:rxbinding:0.2.0'
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    // ----------------------------------

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3'
}

android {
    compileSdkVersion sdkVersion
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "com.morihacky.android.rxjava"
        minSdkVersion 15
        targetSdkVersion sdkVersion
        versionCode 2
        versionName "1.2"
    }

    signingConfigs {
        release {
            storeFile file('../debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
        }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        pickFirst  'META-INF/rxjava.properties'
    }
}